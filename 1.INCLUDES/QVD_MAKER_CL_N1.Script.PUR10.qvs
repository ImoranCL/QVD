//==================================================================================================================================================================
// DOCUMENTO:   QVD_MAKER_QlikRepository_N1.Script.PUR10.qvs
// TABLA:     	/IMO/D_PUR10
// AUTOR:       CLGRUPOINDUSTRIAL
// DESCRIPCIÓN: MM-PUR: Cabecera del documento de compras
//==================================================================================================================================================================

TRACE ###############################;
TRACE ########## 		PUR10	   	 ;
TRACE ###############################;

//Obtenemos el campo fecha
Fecha:
LOAD [UPD_DATE]

From [lib://QlikRepository/1.DATOS_NIVEL_1/RAW_DATA/1.ORIGEN_1/1.QVD/PUR10.qvd] (qvd);

//Seleccionamos la fecha máxima
MaxOldQVD:

LOAD

Max([UPD_DATE]) as Max0UPD_DATE

Resident Fecha;

LET LastExecTime = Date(Peek('Max0UPD_DATE',-1,MaxOldQVD),'YYYYMMDD');

DROP Table Fecha;

DROP Table MaxOldQVD;

// Let ThisExecTime =date(today(), 'YYYYMMDD');

//Cargamos los datos con carga incremental
PUR10:
LOAD RowNum, 
	[0BBP_CTC_ID] as [BBP_CTC_ID], 
	[0CALDAY] as [CALDAY], 
	[0CREATEDBY] as [CREATEDBY], 
	[0CREATEDON] as [CREATEDON], 
	[0CRM_PMNTTR] as [CRM_PMNTTR], 
	[0DATAPAKID] as [DATAPAKID], 
	[0DOCTYPE] as [DOCTYPE], 
	[0DOC_CAT] as [DOC_CAT],  
	[0ENTRY_DATE] as [ENTRY_DATE], 
	[0FISCPER] as [FISCPER], 
	[0FISCVARNT] as [FISCVARNT], 
	[0FISCYEAR] as [FISCYEAR], 
	[0GN_R3_SSY] as [GN_R3_SSY], 
	[0GN_VENDOR] as [GN_VENDOR], 
	[0LOG_SYS_BE] as [LOG_SYS_BE], 
	[0OI_EBELN] as [OI_EBELN],
	[0ORDER_CURR] as [ORDER_CURR], 
	[0PO_STATUS] as [PO_STATUS], 
	[0PSTNG_DATE] as [PSTNG_DATE], 
	[0PURCH_ORG] as [PURCH_ORG], 
	[0RECORD] as [RECORD], 
	[0RECORDMODE] as [RECORDMODE], 
	[0REQTSN] as [REQTSN], 
	[0SOURSYSTEM] as [SOURSYSTEM], 
	[0UPD_DATE] as [UPD_DATE], 
	[0VAL_END] as [VAL_END], 
	[0VAL_START] as [VAL_START],
	FRGKE, 
	FRGRL, 
	FRGZU, 
	Text(CLC_COMPC) AS CLC_COMPC, 
	CLC_FRGGR, 
	CLC_FRGSX, 
	CLC_MERCA, 
	CLC_NV_PA, 
	CLC_PROFG, 
	CLC_UPP_P, 
	CLC_VEND, 
	[0DELIVERIES] as [DELIVERIES], 
	[0EXCHG_RATE] as [EXCHG_RATE], 
	[0INVOICES] as [INVOICES], 
	[0NO_HDR] as [NO_HDR], 
	ZBD1T, 
	ZBD2T,
    [0SOURSYSTEM]&[0OI_EBELN] as UniqueKey;

SELECT		[RowNum],	// RowNum
	[0BBP_CTC_ID],	// ID de contrato
	[0CALDAY],	// Fecha de documento de compras
	[0CREATEDBY],	// Nombre del responsable que ha añadido el objeto
	[0CREATEDON],	// Fecha de creación del registro
	[0CRM_PMNTTR],	// Condiciones de pago
	[0DATAPAKID],	// Paquete de datos
	[0DOCTYPE],	// Clase de documento de compras
	[0DOC_CAT],	// Tipo documento de compras
	[0ENTRY_DATE],	// Fecha de registro del documento de compras
	[0FISCPER],	// Ejercicio/Período
	[0FISCVARNT],	// Variante de ejercicio
	[0FISCYEAR],	// Ejercicio
	[0GN_R3_SSY],	// Sistema fuente de la entidad (2 posiciones)
	[0GN_VENDOR],	// Proveedor
	[0LOG_SYS_BE],	// Sistema lógico back end
	[0OI_EBELN],	// Número del documento de compras
	[0ORDER_CURR],	// Moneda de pedido
	[0PO_STATUS],	// Status del documento de compras
	[0PSTNG_DATE],	// Fecha de contabilización en el documento
	[0PURCH_ORG],	// Organización de compras
	[0RECORD],	// Número registro datos
	[0RECORDMODE],	// BW procedimiento delta: Modo de actualización
	[0REQTSN],	// Número de transacción consecutivo de solicitud
	[0SOURSYSTEM],	// ID sistema fuente
	[0UPD_DATE],	// Fecha de actualización
	[0VAL_END],	// Fin per.val.
	[0VAL_START],	// Inicio del período de validez
	[FRGKE],	// Indicador de liberación
	[FRGRL],	// Liberación incompleta
	[FRGZU],	// Estado de liberación
	[CLC_COMPC],	// Sociedad
	[CLC_FRGGR],	// Grupo de liberación
	[CLC_FRGSX],	// Estrategia de liberación
	[CLC_MERCA],	// Mercado
	[CLC_NV_PA],	// Emisor de la factura
	[CLC_PROFG],	// Grupo de compras
	[CLC_UPP_P],	// Centro suministrador
	[CLC_VEND],	// Proveedor_1
	[0DELIVERIES],	// Cantidad entregas
	[0EXCHG_RATE],	// Tp.cambio para la determinación de precios y estadísticas
	[0INVOICES],	// Cantidad facturas
	[0NO_HDR],	// Contador para documentos
	[ZBD1T],	// Pago en
	[ZBD2T]	// Pago en_1

FROM /IMO/D_PUR10
WHERE (
[0UPD_DATE]  >= $(LastExecTime)
);

[PURCH_ORG_TEXTS]: LEFT JOIN LOAD [PURCH_ORG], [TXTSH] AS [PURCH_ORG_TEXT]; SELECT PURCH_ORG, TXTSH FROM /BI0/TPURCH_ORG;
[CLC_COMPC_TEXTS]: LEFT JOIN LOAD [SOURSYSTEM], Text([/BIC/CLC_COMPC]) AS [CLC_COMPC], [TXTMD] AS [CLC_COMPC_TEXT]; SELECT SOURSYSTEM, /BIC/CLC_COMPC, TXTMD FROM /BIC/TCLC_COMPC;
[CLC_PROFG_TEXTS]: LEFT JOIN LOAD [SOURSYSTEM], [/BIC/CLC_PROFG] AS [CLC_PROFG], [TXTSH] AS [CLC_PROFG_TEXT]; SELECT SOURSYSTEM, /BIC/CLC_PROFG, TXTSH FROM /BIC/TCLC_PROFG;
[CLC_VEND_TEXTS]: LEFT JOIN LOAD [SOURSYSTEM], [/BIC/CLC_VEND] AS [CLC_VEND], [TXTMD] AS [CLC_VEND_TEXT]; SELECT SOURSYSTEM, /BIC/CLC_VEND, TXTMD FROM /BIC/TCLC_VEND;

Concatenate
LOAD
    SOURSYSTEM,
    CLC_VEND,
    CLC_PROFG,
    CLC_COMPC,
    PURCH_ORG,
    RowNum,
    BBP_CTC_ID,
    CALDAY,
    CREATEDBY,
    CREATEDON,
    CRM_PMNTTR,
    DATAPAKID,
    DOCTYPE,
    DOC_CAT,
    ENTRY_DATE,
    FISCPER,
    FISCVARNT,
    FISCYEAR,
    GN_R3_SSY,
    GN_VENDOR,
    LOG_SYS_BE,
    OI_EBELN,
    ORDER_CURR,
    PO_STATUS,
    PSTNG_DATE,
    "RECORD",
    RECORDMODE,
    REQTSN,
    UPD_DATE,
    VAL_END,
    VAL_START,
    FRGKE,
    FRGRL,
    FRGZU,
    CLC_FRGGR,
    CLC_FRGSX,
    CLC_MERCA,
    CLC_NV_PA,
    CLC_UPP_P,
    DELIVERIES,
    EXCHG_RATE,
    INVOICES,
    NO_HDR,
    ZBD1T,
    ZBD2T,
    UniqueKey,
    PURCH_ORG_TEXT,
    CLC_COMPC_TEXT,
    CLC_PROFG_TEXT,
    CLC_VEND_TEXT
FROM [lib://QlikRepository/1.DATOS_NIVEL_1/RAW_DATA/1.ORIGEN_1/1.QVD/PUR10.qvd](qvd)
Where not Exists(UniqueKey);


// Se almacena en QVD la tabla PUR10 y posteriormente se elimina
STORE PUR10 INTO  [lib://QlikRepository/1.DATOS_NIVEL_1/RAW_DATA/1.ORIGEN_1/1.QVD/PUR10.qvd];

DROP Table PUR10;

